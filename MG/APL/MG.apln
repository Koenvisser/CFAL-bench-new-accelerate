:Namespace MG

DS←256 ⋄ IT←4

CI←+⌿¨|RI←,¯1+⍳3 3 3
A←(¯8÷3)  (0)  (÷6)(÷12)[CI]
P←(÷2     4    8     16)[CI]
Q←(÷1     2    4      8)[CI]
Sa←(¯3÷8) (÷32)(÷¯64) 0 [CI]
Sb←(¯3÷17)(÷33)(÷¯61) 0 [CI]
S←Sa

pp←{⍺←'' ⋄ ⍵⊣⎕←⍺(L2 ⍵)}
st←{⊃⍵{k x y z←⍺ ⋄ ⍵+k×x⊖y⌽[1]z⌽⍺⍺}⌿(⍺,¨RI),⊂(⍴⍵)⍴0}
dn←{((≢⍴⍵)⍴⊂1+2×⍳(≢⍵)÷2)⌷⍵}
up←{X⊣X[i;i;i←1+2×⍳≢⍵]←⍵⊣X←0⍴⍨2×⍴⍵}

neg_i← (211 154 98)(102 138 112)(101 156 59)(17 205 32)(92 63 205)
neg_i,←(199 7 203)(250 170 157)(82 184 255)(154 162 36)(223 42 240)
pos_i← (57 120 167)(5 118 175)(176 246 164)(45 194 234)(212 7 248)
pos_i,←(115 123 207)(202 83 209)(203 18 198)(243 172 14)(54 209 40)
v←¯1@neg_i⊢1@pos_i⊢(3⍴DS)⍴0

M←{⍺≤1:'psinv'pp S st ⍵ ⋄ 'psinv'pp z+S st 'resid'pp ⍵-A st⊢z←'inter'pp Q st up(⍺-1)∇'rprj3'pp dn P st ⍵}
L2←{((+⌿,⍵×⍵)÷×⌿⍴⍵)*.5}
MG←{L2 ⍺-A st ⍺{⍵+k M 'resid'pp ⍺-A st ⍵}⍣IT⊢⍵⊣k←2⍟≢⍵}

Run←{
	u←DS DS DS⍴0
	⎕←'Running MG...'
	r←v MG u
	⎕←'MG time elapsed: ',⍕¯1
	r
}

:EndNamespace
