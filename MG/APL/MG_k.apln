:Namespace MG_k

DS←0 ⋄ IT←0 ⋄ k←0

CI←+/|RI←⍉¯1+3 3 3⊤⍳27
A←(¯8÷3)  (0)  (÷6)(÷12)
P←(÷2     4    8     16)
Q←(÷1     2    4      8)
Sa←(¯3÷8) (÷32)(÷¯64) 0 
Sb←(¯3÷17)(÷33)(÷¯61) 0 
S←Sa

st←{k←⍺
	x←⍵×k[0]
	x+←k[1]×(1⊖⍵)+(¯1⊖⍵)+r0←(1⌽⍵)+(¯1⌽⍵)+r01←(1⌽[1]⍵)+¯1⌽[1]⍵
	x+←k[2]×(1⊖r0)+(¯1⊖r0)+r1←(1⌽r01)+¯1⌽r01
	x+←k[3]×(1⊖r1)+¯1⊖r1
	⌷x
}
dn←{⍵[i;i;i←1+2×⍳(≢⍵)÷2]}
up←{X⊣X[i;i;i←1+2×⍳≢⍵]←⍵⊣X←1e¯100⍴⍨2×⍴⍵}

M←{⍺≤1:S st ⍵ ⋄ z+S st ⍵-A st⊢z←Q st up(⍺-1)∇dn P st ⍵}
L2←{((+⌿,⍵×⍵)÷×⌿⍴⍵)*.5}
MG←{⌷L2 ⍺-A st ⍺{⌷⍵+k M ⍺-A st ⍵}⍣IT⊢⍵}

u←⍬ ⋄ v←⍬ ⋄ res←⍬

init←{v⊢←0⊃⍵ ⋄ u⊢←1⊃⍵ ⋄ DS⊢←0⊃⍺ ⋄ IT⊢←1⊃⍺ ⋄ S⊢←(2⊃⍺)⊃Sa Sb ⋄ k⊢←2⍟DS ⋄ res⊢←⍬}
warmup←{⍬⊣v MG u}
run←{⍬⊣res⊢←v MG u}
result←{res}

:EndNamespace
